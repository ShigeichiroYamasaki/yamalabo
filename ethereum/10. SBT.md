# 10. ERC-5192 SBT (SoulBound Token) 譲渡不可能な NFT

2024/12/13
Shigeichiro Yamasaki

## ERC-5192 の仕様

[ERC-5192: Minimal Soulbound NFTs](https://eips.ethereum.org/EIPS/eip-5192)

## ERC-5192 の概要

* ERC-721 NFTを拡張し，単一のアカウントにバインドされたNFTを定義する．
* この ERC はERC-721トークンをソウルバウンドにするための最小限の interface の定義
* EIP-165 の interface 検出機能を使用する



## NFTの移転をロックする方法

* `locked(uint256 tokenId)` という名前の関数を使ってトークンのロック状態を照会できます。
* トークンがロックされている場合、すべてのEIP721関数はトークンの転送を拒否する必要があります。


## ERC5192 コントラクトのインターフェース (IERC5192)

```js
// SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.0;

interface IERC5192 {
  /// @notice Emitted when the locking status is changed to locked.
  /// @dev If a token is minted and the status is locked, this event should be emitted.
  /// @param tokenId The identifier for a token.
  event Locked(uint256 tokenId);

  /// @notice Emitted when the locking status is changed to unlocked.
  /// @dev If a token is minted and the status is unlocked, this event should be emitted.
  /// @param tokenId The identifier for a token.
  event Unlocked(uint256 tokenId);

  /// @notice Returns the locking status of an Soulbound Token
  /// @dev SBTs assigned to zero address are considered invalid, and queries
  /// about them do throw.
  /// @param tokenId The identifier for an SBT.
  function locked(uint256 tokenId) external view returns (bool);
}
```



## hardhat プロジェクトの作成

```bash
cd ~/hardhat
mkdir sbt
cd sbt
```

```bash
yarn init -y
yarn add --dev hardhat
npx hardhat init
```

```bash
// 消さなくても実害は無いが消しておく
rm contracts/Lock.sol
rm test/Lock.js
rm ignition/modules/Lock.js
```


## OpenZeppelinの ERC20 ライブラリのインストール

```bash
yarn add --dev @openzeppelin/contracts
```


## Solidity プログラム

SBT の solidity プログラム

```bash
mkdir contracts/ERC5192
```

```bash
nano contracts/ERC5192/IERC5192.sol
```

```js
// SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.0;

interface IERC5192 {
  /// @notice Emitted when the locking status is changed to locked.
  /// @dev If a token is minted and the status is locked, this event should be emitted.
  /// @param tokenId The identifier for a token.
  event Locked(uint256 tokenId);

  /// @notice Emitted when the locking status is changed to unlocked.
  /// @dev If a token is minted and the status is unlocked, this event should be emitted.
  /// @param tokenId The identifier for a token.
  event Unlocked(uint256 tokenId);

  /// @notice Returns the locking status of an Soulbound Token
  /// @dev SBTs assigned to zero address are considered invalid, and queries
  /// about them do throw.
  /// @param tokenId The identifier for an SBT.
  function locked(uint256 tokenId) external view returns (bool);
}
```


```bash
nano contracts/ERC5192/ERC5192.sol
```

```js
// SPDX-License-Identifier: GPL-3.0-only
pragma solidity ^0.8.13;

import {ERC721} from "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import {IERC5192} from "./IERC5192.sol";

abstract contract ERC5192 is ERC721, IERC5192 {
  bool private isLocked;

  error ErrLocked();
  error ErrNotFound();

  constructor(string memory _name, string memory _symbol, bool _isLocked)
    ERC721(_name, _symbol)
  {
    isLocked = _isLocked;
  }

  modifier checkLock() {
    if (isLocked) revert ErrLocked();
    _;
  }

  function locked(uint256 tokenId) external view returns (bool) {
    require(_ownerOf(tokenId) != address(0), "Token does not exist");
    return isLocked;
  }

  function safeTransferFrom(
    address from,
    address to,
    uint256 tokenId,
    bytes memory data
  ) virtual public override checkLock {
    super.safeTransferFrom(from, to, tokenId, data);
  }

  function safeTransferFrom(address from, address to, uint256 tokenId)
    virtual public
    override
    checkLock
  {
    super.safeTransferFrom(from, to, tokenId);
  }

  function transferFrom(address from, address to, uint256 tokenId)
    public
    override
    checkLock
  {
    super.transferFrom(from, to, tokenId);
  }

  function approve(address approved, uint256 tokenId) public override checkLock {
    super.approve(approved, tokenId);
  }

  function setApprovalForAll(address operator, bool approved)
    public
    override
    checkLock
  {
    super.setApprovalForAll(operator, approved);
  }

  function supportsInterface(bytes4 interfaceId)
    public
    view
    virtual
    override
    returns (bool)
  {
    return interfaceId == type(IERC5192).interfaceId
      || super.supportsInterface(interfaceId);
  }
}
```

* ERC-721

```bash
mkdir contracts/ERC721
```

```bash
nano contracts/ERC721/ERC721.sol
```

```js
```

* SBT

```bash
nano contracts/sbt.sol
```

```js
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.13;

import {ERC5192} from "./ERC5192/ERC5192.sol";

contract SBT is ERC5192 {
  bool private isLocked;
  constructor(string memory _name, string memory _symbol, bool _isLocked)
    ERC5192(_name, _symbol, _isLocked)
  {
    isLocked = _isLocked;
  }
  function safeMint(address to, uint256 tokenId) external {
    _safeMint(to, tokenId);
    if (isLocked) emit Locked(tokenId);
  }
}

```

