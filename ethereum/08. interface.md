# 08. Solidity の interface

2025/02/08

Shigeichiro Yamasaki

* ERC-20 トークンの送金先コントラクトの作成
* IERC20 interface の利用

## Solidity の interface とは何か？

web3 アプリケーションのトークンは流通性を持たないと意味がありません．
そのために，ERC-20 や ERC-721 などの標準化されたトークンが重要になります．
標準化されたトークンはメタマスクなどの一般的なワレットで扱うことが可能だからです．
web3 アプリケーションは，これらの標準化されたトークンのコントラクトを継承することによって流通性をもたせることができます．

しかし，実際のトークンはアプリケーションによって様々な拡張機能を持ちます．そのような拡張機能を流通性を維持しながら有効利用する方法が必要になります．その方法が interface です．

ERC-20 の場合には，IERC-20 という openzeppelinによるインターフェースが存在します．ERC-721 には ERC-165 などのインターフェースが定義されています．また ERC-820 というレジストリ型のインターフェスの仕様も存在します．

### 既存の（古い）プログラムから新しいプログラムを利用する方法

既存のライブラリを利用して新しいプログラムを作成するのは簡単です．継承はそのような既存のライブラリを効率的に利用するしくみです．
しかし，既存の（古い）プログラムの側から新しいプログラムを呼び出して利用するのは簡単ではありません．

これを実現する技術は存在します．例えば，画像加工ソフトの photoshop では，画像処理プラグインモジュールでこれを実現しています．新しい画像処理モジュールを photoshop の特定のフォルダーにコピーすると，photoshopのメニューにその機能が追加されて使えるようになります．

PCに新しいプリンタを接続したときに，OSにデバイスドライバを組み込むと任意のアプリケーションの「プリント」メニューからそのプリンタで印刷できるようになるのも同様です．

こういった既存のプログラム（photoshopやOS）から新しいプログラム（プラグインモジュールやデバイスドライバ）が利用できるのは，既存のプログラムに画像加工やプリントのための標準インターフェースが定義されているからです．

### web3 アプリにおけるトークンへの interface の定義の重要性

* トークンの流通性
  
トークンはお金と同様にアプリケーションやサービスの境界を超えて広域的に流通します．
新しいトークンのために多数のアプリケーションやサービスがそれに対応してくれることを望むのは現実的ではありません．

* 既存アプリケーションからのトークンの新規機能の利用とweb3
  
既存のアプリケーションが，トークンの新しい機能の存在を知り，それを呼び出し，その結果を得るためのインターフェースが存在していることは，トークンの機能を中心にしたweb3 アプリケーションが，サーバー型の web2 アプリケーションと本質的に異なる点の一つです．


## Solidity の interface の条件

* 他のコントラクトから継承することはできませんが、他のインターフェースから継承することはできます。
* 宣言されたすべての関数は、コントラクト内で public であっても、interface 内では external である必要があります。
* コンストラクターを宣言することはできません。
* 状態変数を宣言することはできません。
* 修飾子を宣言することはできません。

## interface の例

### harfhat プロジェクト

```bash
cd ~/hardhat
mkdir interface
cd interface

yarn init -y
yarn add --dev hardhat
npx hardhat init

rm contracts/Lock.sol
rm test/Lock.js
rm ignition/modules/Lock.js
```

### Counter interface を試すコントラクト

```bash
nano contracts/counter.sol
```

```js
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.26;

// Counter コントラクトへのインターフェース
interface ICounter {
    // 現在のカウントの値を返す関数のインターフェース
    function count() external view returns (uint256);
    // 状態変数をカウントアップする関数のインターフェース
    function increment() external;
}

// Counter コントラクトのインターフェースを備えたコントラクト
contract MyContract {
    // interface を介して Counter コントラクトの関数を利用してカウントアップする
    function incrementCounter(address _counter) external {
        ICounter(_counter).increment();
    }
    // interface を介して Counter コントラクトの状態変数の値を得る
    function getCount(address _counter) external view returns (uint256) {
        return ICounter(_counter).count();
    }
}

// Counter コントラクトの実装
contract Counter {
    uint256 public count;
    // 状態変数 count の値をカウントアップする関数
    function increment() external {
        count += 1;
    }
}


```

### コンパイル

```bash
npx hardhat compile
```

## interface のテスト

* 上記の MyContract コントラクトには Counter コントラクトへの interface が定義されているだけで，実際のカウンター機能は実装されていません．
* MyContract は デプロイされた Counter コントラクトのコントラクトアドレスを介して Counter コントラクトの increment 関数を呼び出すことができます．
* コントラクトアドレスは，`.target` メソッドを使って参照することができます

```bash
nano test/counter.js
```

```js
// hardhat tool box の利用
const {loadFixture} = require("@nomicfoundation/hardhat-toolbox/network-helpers");
// CHaiの利用
const { expect } = require("chai");

describe("Counterコントラクトのinterfaceの利用", function () {
     async function deployMyContractFixture() {
        const Counter = await ethers.deployContract("Counter",[]);
        const MyContract = await ethers.deployContract("MyContract",[]);
        return {Counter, MyContract};
     }
     it("MyContract から Counterコントラクトの関数を利用する", async function () {
      // コントラクトをロードする
      const {Counter, MyContract} = await loadFixture(deployMyContractFixture);
      // Counterコントラクトのコントラクトアドレスによって，Counter コントラクトの関数を使って 2回カウントアップする
      await MyContract.incrementCounter(Counter.target);
      await MyContract.incrementCounter(Counter.target);
      // カウントを参照し，結果が 2n であることを確認する
      expect(await MyContract.getCount(Counter.target)).to.equal(2n);
    });
});

```

## 異なる ERC-20 トークンを扱える faucet を作る

openzeppelin の IERC20 インターフェースを利用する

### OpenZeppelinのライブラリのインストール

```bash
yarn add --dev @openzeppelin/contracts
```

```bash
nano contracts/IERC20Faucet.sol
```

[openzeppelin の IERC20 コード](https://github.com/openzeppelin/openzeppelin-contracts/blob/master/contracts/token/ERC20/IERC20.sol)

```js
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

// OpenZeppelinの IERC20 interface をインポートする
import "@openzeppelin/contracts/interfaces/IERC20.sol";

// IERC20 interface を備えたコントラクト
contract IERC20Faucet {
    address public token; // ERC20トークンコントラクトのアドレス
    address public owner; // 所有者のアドレス
    uint256 public rate;  // 1ETH と交換されるトークンの量（交換比率）

    // 初期化時にのデプロイ済のERC20コントラクトと ETH とトークンの交換比率を設定する
    constructor(uint256 _rate) {
        rate = _rate;
        owner = msg.sender; // コントラクトをデプロイしたアドレスを所有者として設定
    }

    // トークンコントラクトのインスタンスを状態変数に代入する
    function setToken(address _token) public {
        token = _token;
    }
    // トークンの購入を実施する関数
    function buyTokens() public payable {
        // msg.value :この関数を呼ぶトランザクションで送金された ETH の金額（wei単位）
        // この ETH の金額が購入代金になるので，0より大きくなければならない
        require(msg.value > 0, "ETH must be greater than 0");
        // 購入する ETH から交換比率に従って送付するトークン量を計算する
        uint256 tokenAmount = msg.value * rate;
        // 売り手 (owner) に十分なトークンがあるか確認
        require(IERC20(token).balanceOf(owner) >= tokenAmount, "Seller does not have enough tokens");
        // 売り手から購入者へトークンを送金する（トークンの送金にエラーが起きなければ）
        require(IERC20(token).transferFrom(owner, msg.sender, tokenAmount), "Token transfer failed");
    }
    // 売り手(所有者)がETHを引き出すための関数
    function withdrawETH() public {
        require(msg.sender == owner, "Only owner can withdraw");
        payable(owner).transfer(address(this).balance);
    }
    // コントラクトの残高をETH確認する関数
    function getBalance() public view returns (uint256) {
        return address(this).balance;
    }
}
```


### 2種類の ERC-20 トークンのコントラクト

* JPYcoin
* USDcoin

```bash
nano contracts/ERC20Token.sol
```

```js
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

// ERC20 の実装ライブラリ
import "@openzeppelin/contracts/token/ERC20/ERC20.sol";

// JPYcoinの定義
contract JPYcoin is ERC20 {
  // ERC20 コントラクトのコンストラクタの引数 (name, symbol)
    constructor(uint256 initialSupply) ERC20("JPYcoin", "JPQ") {
        _mint(msg.sender, initialSupply);
    }
}
// USDcoinの定義
contract USDcoin is ERC20 {
  // ERC20 コントラクトのコンストラクタの引数 (name, symbol)
    constructor(uint256 initialSupply) ERC20("USDcoin", "USG") {
        _mint(msg.sender, initialSupply);
    }
}
```

### コンパイル

```bash
npx hardhat compile
```

### コンソールからの操作

* ノードの起動

```bash
npx hardhat node
```

* コンソールの起動

```bash
$ npx hardhat console --network localhost 
```

```js
> const [owner, addr1, addr2] = await ethers.getSigners();

> const JPYcoin = await ethers.deployContract("JPYcoin",[10n**11n]);
> const USDcoin = await ethers.deployContract("USDcoin",[10n**11n]);
> const IERC20Faucet = await ethers.deployContract("IERC20Faucet",[20]);


> await IERC20Faucet.setToken(JPYcoin.target)

// ERC20コントラクトからトークン購入コントラクトに対して代理送金を許可する（1000000000トークンまで）
> await JPYcoin.approve(IERC20Faucet,1000000000n);

> await IERC20Faucet.connect(addr1).buyTokens({value: 20000})
> await JPYcoin.balanceOf(addr1.address)
400000n
> await JPYcoin.balanceOf(owner.address)
99999600000n

```

* 対象となる ERC20 トークンを USDcoin に変更する

```js
> await IERC20Faucet.setToken(USDcoin.target)

// ERC20コントラクトからトークン購入コントラクトに対して代理送金を許可する（1000000000トークンまで）
> await USDcoin.approve(IERC20Faucet,1000000000n);

> await IERC20Faucet.connect(addr1).buyTokens({value: 200})
> await USDcoin.balanceOf(addr1.address)
4000n
> await USDcoin.balanceOf(owner.address)
99999996000n
```























## ERC-165 (Standard Interface Detection) とは

スマートコントラクトにどのようなインターフェースが存在しているかを，公開・検知するための規格

* トークンが流通性を持つためには，相互運用性のためにそのトークンへのインターフェースが公開されている必要がある
* ERC-165 に準拠したコントラクトには以下の「インターフェースの存在を確認する関数」が定義されている．

```solidity
supportsInterface()
```

* ERC-5192 (SBT) は ERC-721 を基本にした NFT で， ERC-165 に準拠した最小限のインターフェースを定義可能にしたものです．
* SBT は，NFT の移転ができないように，移転のロックができる機能を備えています．
